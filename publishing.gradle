task delombokedSourcesJars(type: Jar, dependsOn: classes) {
    from sourceSets.main.delombokTask {
        include "**/*.java"
        filter { String line -> line.replaceAll('^\\s*import lombok\\..*$', '') }
    }
    from('src/main/resources') {
        include '**/*'
    }
    from {
        configurations.sourceLibsTransitive.collect { it.isDirectory() ? it : zipTree(it) }
    }
    archiveClassifier.set("sources")
}

javadoc {
    source = sourceSets.main.delombokTask
    failOnError = false
}

java {
    withJavadocJar()
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.xmlLibs.collect {
            it.isDirectory() ? it : zipTree(it).matching {
                include "**/*.gwt.xml"
            }
        }
    }
}

publishing {
    publications {
        gdxUnBox2d(MavenPublication) {
            pom {
                name = "gdx-UnBox2D"
                description = "A libGDX library to couple Unity's behaviour system and execution order with Box2D."
                url = "https://github.com/lyze237/gdx-UnBox2D"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
            from components.java
            artifact delombokedSourcesJars
        }
    }
}
